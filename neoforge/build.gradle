import com.modrinth.minotaur.TaskModrinthUpload
import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "net.darkhax.curseforgegradle" version "1.1.+"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    runs {
        configureEach {
            programArgs "--all", "--mod", "${mod_id}"
            programArgs "--output", project(":common").file("src/main/generated").absolutePath
            programArgs "--existing", file("src/main/resources").absolutePath
            programArgs "--existing", file("src/main/generated").absolutePath
            programArgs "--existing", project(":common").file("src/main/resources").absolutePath
            programArgs "--existing", project(":common").file("src/main/generated").absolutePath
        }

        clientData {
            clientData()
            environment("dataclient")
            forgeTemplate("dataclient")
        }

        serverData {
            serverData()
            environment("dataserver")
            forgeTemplate("dataserver")
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

repositories {
    maven { url = "https://maven.neoforged.net/releases/" }
}

dependencies {
    neoForge "net.neoforged:neoforge:${neo_version}"
    modApi "dev.architectury:architectury-neoforge:${arch_version}"

    modApi "me.shedaniel.cloth:cloth-config-neoforge:${cloth_version}"
//    modRuntimeOnly "mezz.jei:jei-${mc_version}-neoforge:${jei_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    atAccessWideners.add("${mod_id}.accesswidener")
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

task publishCurseForge(type: TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(curseforge_id, remapJar)
    mainFile.displayName = "${mod_id}-${mc_version}-${mod_version} [NeoForge]"
    mainFile.releaseType = release_type
    mainFile.changelog = "${project.property("changelog")}"
    mainFile.changelogType = "markdown"
    mainFile.addGameVersion(mc_version)
    mainFile.addModLoader("neoforge")
    mainFile.addRequirement("architectury-api")
    mainFile.addRequirement("cloth-config")
    mainFile.addRequirement("terrablender-neoforge")
}

task publishModrinth(type: TaskModrinthUpload) {
    modrinth {
        token = System.getenv("MODRINTH_TOKEN")
        projectId = modrinth_id
        versionName = "${mod_name} ${mc_version}-${mod_version} [NeoForge]"
        versionNumber = "${mod_version}+neoforge"
        versionType = release_type
        changelog = "${project.property("changelog")}"
        uploadFile = remapJar
        additionalFiles = [sourcesJar]
        gameVersions = [mc_version]
        loaders = ["neoforge"]
        dependencies {
            required.project "architectury-api"
            required.project "cloth-config"
            required.project "terrablender"
        }
    }
}