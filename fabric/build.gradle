plugins {
    id "multiloader-loader"
    id "fabric-loom"
    id "com.hypherionmc.modutils.modpublisher" version "2.+"
}

sourceSets.main.resources.srcDir project(":common").file("src/main/generated").absolutePath

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parch_mc_version}:${parch_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "maven.modrinth:scabbard:${scab_version}+fabric"
    modImplementation "com.github.glitchfiend:TerraBlender-fabric:${mc_version}-${tb_version}"
    modApi("me.shedaniel.cloth:cloth-config-neoforge:${cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi "com.terraformersmc:modmenu:${mm_version}"
}

loom {
    def aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run-server")
        }
    }
}

publisher {
    apiKeys {
        modrinth System.getenv("MODRINTH_TOKEN")
        curseforge System.getenv("CURSEFORGE_TOKEN")
    }
    setCurseID("$curseforge_id")
    setModrinthID("$modrinth_id")
    setVersionType("$release_type")
    setChangelog(projectDir.toPath().parent.resolve("CHANGELOG.md").toFile().readLines().join("\n"))

    setDisplayName("$mod_name ${project.version} [Fabric]")
    setVersion("${mod_version}+fabric")
    setGameVersions("$mc_version")
    setLoaders("fabric")

    setArtifact(jar)
    addAdditionalFile(sourcesJar)
    setCurseEnvironment("both")

    curseDepends {
        required "scabbard", "cloth-config", "terrablender-fabric"
    }
    modrinthDepends {
        required "scabbard", "cloth-config", "terrablender"
    }
}